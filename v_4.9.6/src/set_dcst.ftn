copyright (C) 2001  MSC-RPN COMM  %%%MC2%%%
***function set_dcst - initializes geophysical constants
*
      logical function set_dcst (val,liste,nbre,iun,lun_out)
*
      implicit none
*
      integer nbre,iun,lun_out
      character * (*) liste(nbre)
      real val(nbre)
*
*author
*     M. Desgagne
*
*revision
*
*language
*       fortran 77
*
*object
*	This subroutine reads the file containing the unified
*	CMC-RPN constants to initialize the common starting
*       with val(1) from the interface.
*
*arguments
*
*implicits
#include "partopo.cdk"
*
**
      character*10  rdname
      character*42  text
      character*256 chemin,varenv,fname
      logical flag(nbre),cte_ok
      integer i, ier, longueur
      real*8 rdval
      external longueur
      include 'mpif.h'
*
*     ---------------------------------------------------------------
*
*C       1.    initialization of the common of constants for dynamics
*              ------------------------------------------------------
*
      if (lun_out.gt.0) then
*
      write (lun_out,1000)
      do i=1,nbre
         flag(i) = .false.
      end do
*
      fname  = 'constantes'
      chemin = ' '
*
#if ! defined (T3E)
      varenv = 'AFSISIO'
      call getenvc (varenv,chemin)
      chemin(longueur(chemin)+1:len(chemin)) = '/datafiles/constants/'
      chemin(longueur(chemin)+1:len(chemin)) = 'thermoconsts'
#endif
*
      open(iun,file=fname,iostat=ier,form='FORMATTED',status='OLD',
     $                                         access='SEQUENTIAL')
*
      if (ier.ne.0) then
         write (lun_out,2000) chemin(1:longueur(chemin)),' (AFSISIO)'
         open(iun,file=chemin,iostat=ier,form='FORMATTED',status='OLD',
     $                                             access='SEQUENTIAL')
      else
         write (lun_out,2000) fname(1:longueur(fname)),' (local)'
      endif
*
      if (ier.eq.0) then
 100     read (iun,'(2x,a10,2x,e20.13,2x,a42)',end=2) rdname,rdval,text
         do i=1,nbre
            if (liste(i).eq.rdname) then
               val(i) = rdval
               flag(i)= .true.
               if (liste(i).eq.'KARMAN') val(i) = 0.4
               goto 100
            endif
         end do
         goto 100
 2       close (iun)
      endif
*
      cte_ok = .true.
*
      do i=1,nbre
         if (flag(i)) then
            write (lun_out,1005) liste(i),val(i)
         else            
            write (lun_out,1006) liste(i)
            cte_ok = .false.
         endif
      end do
*
      write (lun_out,*)
*
      endif
*
      if (numproc.gt.1) then
      call MPI_bcast (cte_ok ,1,MPI_LOGICAL  ,0,MPI_COMM_WORLD,ier )
      if (cte_ok)
     $     call MPI_bcast (val ,nbre,MPI_REAL   ,0,MPI_COMM_WORLD,ier )
      endif
*
      set_dcst = cte_ok
*
 1000 format(
     +/,'INITIALIZATION OF PHYSICAL CONSTANTS: (S/R SET_DCST)',
     +/,'====================================================')
 1005 format(1x,"THE VALUE OF",1x,a10,2x,"=",1x,e16.7)
 1006 format(" WARNING ==> THE CONSTANT ",a10," DOES NOT EXIST.")
 2000 format (2a)
*
*     ---------------------------------------------------------------
*
      return
      end
